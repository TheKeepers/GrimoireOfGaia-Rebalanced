buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1+"
    }
}

plugins {
    id "com.jfrog.bintray" version "1.8.1"
    id "org.sonarqube" version "2.6.2"
}

apply plugin: 'net.minecraftforge.gradle.forge'

version = "${mod_version}"
						//." + getBuildNumber() + getStable()
group= "${package_group}"
archivesBaseName = "${mod_name}-${mc_version}"

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8 // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8
}

minecraft {
    version = mc_version + "-" + forge_version
    runDir = "run"

    // the mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   snapshot are built nightly.
    // stable_#            stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not always work.
    // simply re-run your setup task after changing the mappings to update your workspace.
    mappings = mappings_version

    replace '@VERSION@', project.version
    //replaceIn 'LibMisc.java' //I may have missed another file, though I can only find it in here. 
	//^I believe this part was just copied from Botania

    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
}

repositories {
    maven {
        name 'jei'
        url 'http://dvs1.progwml6.com/files/maven'
    }
    maven {
        name 'Curse Maven' // Baubles + Thaumcraft
        url 'http://cursemaven.com'
    }
	maven {
        name = "thiakil"
        url = "http://maven.thiakil.com"
	}
}

dependencies {
    deobfCompile "mezz.jei:jei_1.12.2:4.16.1.302"
    deobfCompile 'curse.maven:baubles-227083:2518667'
    deobfCompile 'curse.maven:thaumcraft-223628:2629023'
}

processResources
{
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.compilerArgs.add('-Xlint:unchecked')
}

def getBuildNumber() {
    if (System.getenv("TRAVIS_BUILD_NUMBER") != null) {
        return System.getenv("TRAVIS_BUILD_NUMBER").toString()
    }
    return ""
}

def getStable() {
    if ((System.getenv("DRONE_BRANCH") == null || System.getenv("DRONE_BRANCH").endsWith("-dev"))
            && (System.getenv("TRAVIS_BRANCH") == null || System.getenv("TRAVIS_BRANCH").endsWith("-dev"))) {
        return "unstable"
    }
    return ""
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publish = true
    filesSpec {
        from 'build/libs'
        into '.'
        exclude '*sources.jar'
    }
    pkg {
        version {
            name = "${project.version}"
            released = new Date()
        }
        repo = 'maven'
        name = 'GrimoireOfGaia'
        licenses = ['CC0-1.0']
        vcsUrl = 'https://github.com/P3pp3rF1y/GrimoireOfGaia.git'
    }
}